plugins {
    id "com.android.application"
    id "kotlin-android"
    id "kotlin-kapt"
    id "com.google.gms.google-services"
    id "com.google.firebase.crashlytics"
    id "com.github.triplet.play" version "2.7.5"
}

android {
    signingConfigs {
        release {
            keyAlias gradle.ext.storeKeyAlias
            keyPassword gradle.ext.storeKeyAliasPassword
            storeFile file(gradle.ext.storeKeyFile)
            storePassword gradle.ext.storeKeyPassword
        }
    }
    compileSdkVersion androidConfig.compileSdkVersion
    defaultConfig {
        applicationId 'net.thebix.debts'
        minSdkVersion androidConfig.minSdkVersion
        targetSdkVersion androidConfig.targetSdkVersion
        versionCode 20
        versionName "2.1.0.$versionCode"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
        defaultConfig {
            manifestPlaceholders = [
                    crashlytics: gradle.ext.fabricApiKey
            ]
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
        }
        release {
            shrinkResources true
            minifyEnabled true
            zipAlignEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    // used by Room, to test migrations
    sourceSets {
        androidTest.assets.srcDirs +=
                files("$projectDir/schemas".toString())
    }

    // TODO: enable lint local checks and remove this
    lintOptions {
        abortOnError false
    }
}

dependencies {
    implementation libs.androidxAppCompat
    implementation libs.androidxConstraintLayout
    implementation libs.androidxCore
    implementation libs.androidxDesign
    kapt libs.androidxLifecycleCompiler
    implementation libs.androidxPreference
    implementation libs.crashlytics
    implementation libs.firebaseCore
    implementation libs.glide
    kapt libs.glideCompiler
    implementation libs.koin
    implementation libs.koinScope
    implementation libs.koinViewModel
    implementation libs.koptional
    implementation libs.kotlin
    debugImplementation libs.leakCanary
    implementation libs.room
    kapt libs.roomCompiler
    implementation libs.roomRxJava
    testImplementation libs.roomTesting
    implementation libs.rxAndroid
    implementation libs.rxBinding
    implementation libs.rxJava2
    implementation libs.timber

    debugImplementation libs.hyperionCore
    debugImplementation libs.hyperionAttr
    debugImplementation libs.hyperionBuildConfig
    debugImplementation libs.hyperionCrash
    debugImplementation libs.hyperionDisk
    debugImplementation libs.hyperionGeigerCounter
    debugImplementation libs.hyperionMeasurement
    debugImplementation libs.hyperionPhoenix
    debugImplementation libs.hyperionRecorder
    debugImplementation libs.hyperionSharedPrefs
    debugImplementation libs.hyperionTimber
}

play {
    releaseName = "Internal track release name"
    userFraction = 1.0
    defaultToAppBundles = true
    serviceAccountCredentials = file("google-play-publisher.json")

    // defaults, just to make explicit
    track = "internal"
    releaseStatus = "completed"
}
