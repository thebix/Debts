apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'io.fabric'

android {
    signingConfigs {
        release {
            keyAlias DEBTS_STORE_KEY_ALIAS
            keyPassword DEBTS_STORE_KEY_ALIAS_PASSWORD
            storeFile file(DEBTS_STORE_KEY_FILE)
            storePassword DEBTS_STORE_KEY_PASSWORD
        }
    }
    compileSdkVersion androidConfig.compileSdkVersion
    buildToolsVersion '28.0.3'
    defaultConfig {
        applicationId 'net.thebix.debts'
        minSdkVersion androidConfig.minSdkVersion
        targetSdkVersion androidConfig.targetSdkVersion
        // TODO: increase version to 2.0.0 before release
        versionCode 10
        versionName '1.0.3'

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
        defaultConfig {
            manifestPlaceholders = [
                    crashlytics: DEBTS_FABRIC_API_KEY
            ]
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
        }
        release {
            shrinkResources true
            minifyEnabled true
            zipAlignEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    // used by Room, to test migrations
    sourceSets {
        androidTest.assets.srcDirs +=
                files("$projectDir/schemas".toString())
    }
}

dependencies {
    implementation libs.androidxAppCompat
    implementation libs.androidxConstraintLayout
    implementation libs.androidxCore
    implementation libs.androidxDesign
    kapt libs.androidxLifecycleCompiler
    implementation libs.androidxPreference
    // TODO: remove this dependency
    implementation libs.circleimageview
    implementation libs.crashlytics
    implementation libs.firebaseCore
    implementation libs.glide
    kapt libs.glideCompiler
    implementation libs.koin
    implementation libs.koinScope
    implementation libs.koinViewModel
    implementation libs.koptional
    implementation libs.kotlin
    debugImplementation libs.leakCanary
    releaseImplementation libs.leakCanaryNoOp
    debugImplementation libs.leakCanarySupportFragment
    implementation libs.room
    kapt libs.roomCompiler
    implementation libs.roomRxJava
    testImplementation libs.roomTesting
    implementation libs.rxAndroid
    implementation libs.rxBinding
    implementation libs.rxJava2
    implementation libs.timber
}
