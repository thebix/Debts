plugins {
    id "com.android.application"
    id "kotlin-android"
    id "kotlin-kapt"
    id "com.google.firebase.crashlytics"
    id "com.google.gms.google-services"
    id "com.github.triplet.play" version "2.7.5"
}

kotlin {
    jvmToolchain {
        languageVersion.set(JavaLanguageVersion.of("$java_version"))
    }
}

android {
    signingConfigs {
        release {
            keyAlias gradle.ext.storeKeyAlias
            keyPassword gradle.ext.storeKeyAliasPassword
            storeFile file(gradle.ext.storeKeyFile)
            storePassword gradle.ext.storeKeyPassword
        }
    }
    compileSdkVersion androidConfig.compileSdkVersion
    defaultConfig {
        applicationId 'net.thebix.debts'
        minSdkVersion androidConfig.minSdkVersion
        targetSdkVersion androidConfig.targetSdkVersion
        versionCode appVersion.code
        versionName appVersion.name

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
        // region This section is added because com.android.tools.build:gradle:7.4.2 is used
        // this region should be removed completely when plugin is updated to 8.1.0-alpha09 and upper
        // link: https://kotlinlang.org/docs/gradle-configure-project.html#gradle-java-toolchains-support
        compileOptions {
            sourceCompatibility = JavaVersion.VERSION_15
            targetCompatibility = JavaVersion.VERSION_15
        }
        // endregion

        println "App Id=$applicationId\nApp code version=${versionCode}\nApp name=$versionName"
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
        }
        release {
            shrinkResources true
            minifyEnabled true
            zipAlignEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    // used by Room, to test migrations
    sourceSets {
        androidTest.assets.srcDirs +=
                files("$projectDir/schemas".toString())
    }
    namespace 'net.thebix.debts'
    // TODO: enable lint local checks and remove this
    lint {
        abortOnError false
    }
}

dependencies {
    implementation libs.androidxAppCompat
    implementation libs.androidxConstraintLayout
    implementation libs.androidxCore
    implementation libs.androidxDesign
    kapt libs.androidxLifecycleCompiler
    implementation libs.androidxPreference
    implementation platform(libs.firebaseBom)
    implementation libs.firebaseCrashlytics
    implementation libs.firebaseAnalytics
    implementation libs.glide
    kapt libs.glideCompiler
    implementation libs.koin
    implementation libs.kotlin
    debugImplementation libs.leakCanary
    implementation libs.room
    kapt libs.roomCompiler
    implementation libs.roomRxJava
    testImplementation libs.roomTesting
    implementation libs.rxAndroid
    implementation libs.rxBinding
    implementation libs.rxJava2
    implementation libs.timber

    debugImplementation libs.hyperionCore
    debugImplementation libs.hyperionAttr
    debugImplementation libs.hyperionBuildConfig
    debugImplementation libs.hyperionDisk
    debugImplementation libs.hyperionGeigerCounter
    debugImplementation libs.hyperionMeasurement
    debugImplementation libs.hyperionPhoenix
    debugImplementation libs.hyperionRecorder
    debugImplementation libs.hyperionSharedPrefs
    debugImplementation libs.hyperionTimber
}

play {
    releaseName = "Internal track release name"
    userFraction = 1.0
    defaultToAppBundles = true
    serviceAccountCredentials = file("google-play-publisher.json")

    // defaults, just to make explicit
    track = "internal"
    releaseStatus = "completed"
}
